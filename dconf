#!/usr/local/bin/perl

# Copyright (c) 2013 Don Owens <don@regexguy.com>.  All rights reserved.
#
# This software is released under the BSD license:
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions
# are met:
#
#  * Redistributions of source code must retain the above copyright
#    notice, this list of conditions and the following disclaimer.
#
#  * Redistributions in binary form must reproduce the above
#    copyright notice, this list of conditions and the following
#    disclaimer in the documentation and/or other materials provided
#    with the distribution.
#
#  * Neither the name of the author nor the names of its
#    contributors may be used to endorse or promote products derived
#    from this software without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
# "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
# LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS
# FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE
# COPYRIGHT OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT,
# INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
# (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
# SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
# HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
# STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
# ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED
# OF THE POSSIBILITY OF SUCH DAMAGE.

# Prints the requested value or values from a JSON config file.
# Run without arguments, or with --help for usage information.

# TODO: update to support bencode, ini, etc., as well as JSON
# (and rename to dconf)

use strict;
use warnings;
use Getopt::Long qw(:config no_ignore_case bundling);
use Data::Dumper ();

our $VERSION = "0.2";

# main
{
    my $self = bless { };

    my $opts = $self->get_options([ "help|h",
                                    "version|V",
                                    "in_file|infile|i|conf|conffile|conf-file|file|c=s",
                                    "stdin",
                                    "out_file|outfile|o=s",
                                    "field|f=s",
                                    "is=s",
                                    "os=s",
                                    "n",
                                    "pod|man",
                                    "self_install|selfinstall|self-install",
                                    "prefix=s",
                                    "bindir=s",
                                    "lines",
                                    "test|self-test",
                                    "eol=s",
                                    "eol_hex|eol-hex=s",
                                    "json",
                                    "bencode",
                                  ], { });
    $self->check_options($opts, [ ]); # die's on bad options
    $self->{opts} = $opts;

    if ($opts->{version}) {
        print STDERR $self->get_full_version;
        exit 0;
    }

    if ($opts->{self_install}) {
        $self->self_install($opts->{prefix}, $opts->{bindir});
        exit 0;
    }

    if ($opts->{pod}) {
        $self->do_pod;
        exit 0;
    }

    if ($opts->{json}) {
        $self->{fmt} = 'json';
    }
    elsif ($opts->{bencode}) {
        $self->{fmt} = 'bencode';
    }

    #$self->setup_json;

    if ($ENV{HARNESS_ACTIVE} and $ENV{DCONFINI_TEST_ACTIVE}) {
        $self->self_test;
        exit 0;
    }

    if ($opts->{test}) {
        $self->run_self_test;
        exit 0;
    }

    unless (defined $opts->{in_file} or $opts->{stdin}) {
        unless (@ARGV > 0) {
            print STDERR "dconf: 'in_file' option required\n";
            exit 1;
        }

        $opts->{in_file} = shift @ARGV;
    }

    unless (defined $opts->{field}) {
        unless (@ARGV > 0) {
            print STDERR "dconf: 'field' option required\n";
            exit 1;
        }

        $opts->{field} = shift @ARGV;
    }

    my $in_fh;
    my $out_fh;

    $self->setup_io(\$in_fh, \$out_fh, $opts);

    $self->do_main($opts, $in_fh, $opts->{field}, $out_fh);
}

exit 0;

###############################################################################
# Subroutines

sub do_main {
    my ($self, $opts, $in_fh, $field, $out_fh) = @_;

    if ($opts->{keys}) {
        $self->{keys} = { };
    }

    if ($opts->{lines}) {
        my $eol;
        if (defined $opts->{eol}) {
            $eol = $opts->{eol};
        }
        elsif (defined $opts->{eol_hex}) {
            $eol = chr(hex($opts->{eol_hex}));
        }
        else {
            $eol = "\n";
        }

        local $/ = $eol;
        while (my $str = <$in_fh>) {
            chomp $str;
            my $conf = $self->deserialize($str);
            $self->process($opts, $field, $out_fh, $conf);
        }
    }
    else {
        my $conf = $self->load_file($in_fh);

        $self->process($opts, $field, $out_fh, $conf);
    }

    if ($opts->{keys}) {
        my @keys = sort keys %{$self->{keys}};
        my $os = defined $opts->{os} ? $opts->{os} : "\n";

        if ($opts->{counts}) {
            my @k = @keys;
            @keys = ();
            foreach my $k (@k) {
                my $v = $self->{keys}{$k};
                $v = '' unless defined $v;
                push @keys, "$k=$v";
            }
        }

        print $out_fh join($os, @keys);
        print $out_fh "\n" if $opts->{n};
    }
}

sub deserialize {
    my $self = shift;

    my $func = $self->{deserialize_func};
    unless (defined $func) {
        $func = $self->build_serialize_func;
        $self->{deserialize_meth} = $func;
    }

    return $func->($_[0]);

    # if ($self->{json_mod} eq 'JSON::DWIW') {
    #     my $conf = JSON::DWIW::deserialize($str);
    #     unless (defined $conf) {
    #         print STDERR "dconfjson: JSON error: "
    #             . JSON::DWIW::get_error_string() . "\n";
    #         exit 1;
    #     }

}

sub switch_data_format {
    my ($self, $fmt) = @_;

    delete $self->{deserialize_func};
    $self->{fmt} = $fmt;
}

sub build_serialize_func {
    my ($self) = @_;

    my $func;

    my $fmt = $self->{fmt};

    if ($fmt eq 'json') {
        eval "use JSON::DWIW;";
        $func = sub { JSON::DWIW::deserialize($_[0]) };
    }
    elsif ($fmt eq 'bencode') {
        eval "use Convert::Bencode_XS ()";
        $func = sub { Convert::Bencode_XS::bdecode($_[0]); };
    }
    else {
        die "data format not specified";
    }

    return $func;
}

sub process {
    my ($self, $opts, $field, $out_fh, $conf) = @_;

    my @fields;
    if (defined $opts->{is}) {
        @fields = split quotemeta($opts->{is}), $field;
        $opts->{os} = $opts->{is} unless defined $opts->{os};
    }
    else {
        @fields = ($field);
    }

    # print STDERR Data::Dumper->Dump([ \@fields ], [ 'fields' ]) . "\n\n";
    # print STDERR Data::Dumper->Dump([ $conf ], [ 'conf' ]) . "\n\n";


    my @vals;
    foreach my $f (@fields) {
        my $val = $self->extract_field($conf, $f);
        $val = '' unless defined $val;

        if ($opts->{keys}) {
            unless (UNIVERSAL::isa($val, 'HASH')) {
                warn "$self->{prog}: '$f' not a leaf node -- skipping";
                next;
            }
        }
        else {
            if (UNIVERSAL::isa($val, 'HASH')) {
                warn "$self->{prog}: '$f' not a leaf node";
                $val = '';
            }
        }

        push @vals, $val;
    }

    if ($opts->{keys}) {
        foreach my $val (@vals) {
            foreach my $k (keys %$val) {
                $self->{keys}{$k} += 1;
            }
        }
    }
    else {
        if (defined $opts->{os}) {
            print $out_fh join($opts->{os}, @vals);
        }
        else {
            print $out_fh @vals;
        }

        print $out_fh "\n" if $opts->{n} or $opts->{lines};
    }
}

sub extract_field {
    my ($self, $conf, $field) = @_;

    my @hier = split /\./, $field;

    my $data = $conf;
    for my $f (@hier) {
        if ($f =~ /^\[(-?\d+)\]$/) {
            my $index = $1;
            return undef unless defined $data
                and UNIVERSAL::isa($data, 'ARRAY');
            if ($index < 0) {
                my $new_index = scalar(@$data) - $index;
                if ($new_index < 0) {
                    $new_index = 0;
                }

                $index = $new_index;
            }

            $data = $data->[$index];
        }
        else {
            return undef unless defined $data
                and UNIVERSAL::isa($data, 'HASH');
            $data = $data->{$f};
        }
    }

    return $data;
}

sub load_file {
    my ($self, $in_fh) = @_;

    my $str;
    {
        local $/;
        $str = <$in_fh>;
    }

    return $self->deserialize($str);
}

sub setup_io {
    my ($self, $in_fh, $out_fh, $opts) = @_;

    if (defined $opts->{in_file}) {
        open($$in_fh, '<', $opts->{in_file})
            or die "couldn't open input file $opts->{in_file}";
    }
    else {
        $$in_fh = *STDIN;
    }

    if (defined $opts->{out_file}) {
        open($$out_fh, '>', $opts->{out_file})
            or die "couldn't open output file $opts->{out_file}";
    }
    else {
        $$out_fh = *STDOUT;
    }

}

sub setup_json {
    my ($self) = @_;

    eval 'use JSON::DWIW';
    unless ($@) {
        $self->{json_mod} = 'JSON::DWIW';
        return 1;
    }

    print STDERR "\ndconfjson: ";
    print STDERR "Couldn't find JSON module to use.  Exiting ...\n\n";
    exit 1;
}

sub run_self_test {
    my ($self) = @_;

    my $perl = $self->get_perl_exec;
    my $me = __FILE__;

    $ENV{DCONFINI_TEST_ACTIVE} = 1;

    (my $sf_me = $me) =~ s/'/\\'/g;
    my @cmd = ($perl, '-MTest::Harness', '-e', "runtests('$sf_me')");
    # my @cmd = ($perl, "-MExtUtils::Command::MM", "-e", "test_harness(0)", $me);

    exec {$cmd[0]} @cmd;

    exit 0;
}

sub self_test {
    my ($self) = @_;

    eval 'use Test::More tests => 12';

    my $inputs = { json => [
                            qq{{"stuff": 3, "db": { "foo": { "bar": "stuff" }, "more": 1, "stuff": 2 } } },

                            qq{["foo", {"stuff": 3, "db": { "foo": { "bar": "stuff" }, "more": 1, "stuff": 2 } }] },

                            qq{{"stuff": 3, "db": { "foo": { "bar": "stuff" }, "more": 1, "stuff": 2 }}
{"stuff": 3, "db": { "foo": { "bar": "stuff" }, "more": 2, "stuff": 2 }}
{"stuff": 3, "db": { "foo": { "bar": "stuff" }, "more": 3, "stuff": 2 }} },

                            qq{{"stuff": 3, "db": { "foo": { "bar": "stuff" }, "more": 1, "stuff": 2 }}::::{"stuff": 3, "db": { "foo": { "bar": "stuff" }, "more": 2, "stuff": 2 }}::::{"stuff": 3, "db": { "foo": { "bar": "stuff" }, "more": 3, "stuff": 2 }} },

                           ],

                   bencode => [
                               'd2:dbd3:food3:bar5:stuffe4:morei1e5:stuffi2ee5:stuffi3ee',
                               'l3:food2:dbd3:food3:bar5:stuffe4:morei1e5:stuffi2ee5:stuffi3eee',
                               "d2:dbd3:food3:bar5:stuffe4:morei1e5:stuffi2ee5:stuffi3ee\nd2:dbd3:food3:bar5:stuffe4:morei2e5:stuffi2ee5:stuffi3ee\nd2:dbd3:food3:bar5:stuffe4:morei3e5:stuffi2ee5:stuffi3ee",

                               "d2:dbd3:food3:bar5:stuffe4:morei1e5:stuffi2ee5:stuffi3ee::::d2:dbd3:food3:bar5:stuffe4:morei2e5:stuffi2ee5:stuffi3ee::::d2:dbd3:food3:bar5:stuffe4:morei3e5:stuffi2ee5:stuffi3ee",

                              ],
                 };

    $self->run_tests_for_format('json', $inputs->{json});
    $self->run_tests_for_format('bencode', $inputs->{bencode});

}

sub run_tests_for_format {
    my ($self, $fmt, $input) = @_;

    $self->switch_data_format($fmt);

    my $conf_str = $input->[0];
    my $conf_str2 = $input->[1];
    my $conf_str3 = $input->[2];
    my $conf_str4 = $input->[3];

    my $out_str = '';
    open(my $in_fh, '<', \$conf_str)
        or die "couldn't create filehandle for string";
    open(my $out_fh, '>', \$out_str);

    $self->do_main({ }, $in_fh, 'db.more', $out_fh);

    close $in_fh;
    close $out_fh;

    is($out_str, '1', "simple check");

    $out_str = '';
    undef $in_fh;
    undef $out_fh;
    open($in_fh, '<', \$conf_str)
        or die "couldn't create filehandle for string";
    open($out_fh, '>', \$out_str);

    $self->do_main({ }, $in_fh, 'db.foo.bar', $out_fh);

    close $in_fh;
    close $out_fh;

    is($out_str, 'stuff', "deeper level");

    $out_str = '';
    undef $in_fh;
    undef $out_fh;
    open($in_fh, '<', \$conf_str)
        or die "couldn't create filehandle for string";
    open($out_fh, '>', \$out_str);

    $self->do_main({ }, $in_fh, 'stuff', $out_fh);

    close $in_fh;
    close $out_fh;

    is($out_str, '3', "global level");

    $out_str = '';
    undef $in_fh;
    undef $out_fh;
    open($in_fh, '<', \$conf_str2)
        or die "couldn't create filehandle for string";
    open($out_fh, '>', \$out_str);

    $self->do_main({ }, $in_fh, '[1].db.foo.bar', $out_fh);

    close $in_fh;
    close $out_fh;

    is($out_str, 'stuff', "deeper level with array access");


    $out_str = '';
    undef $in_fh;
    undef $out_fh;
    open($in_fh, '<', \$conf_str3)
        or die "couldn't create filehandle for string";
    open($out_fh, '>', \$out_str);

    $self->do_main({ lines => 1 }, $in_fh, 'db.more', $out_fh);

    is($out_str, "1\n2\n3\n", "simple check");

    close $in_fh;
    close $out_fh;


    $out_str = '';
    undef $in_fh;
    undef $out_fh;
    open($in_fh, '<', \$conf_str4)
        or die "couldn't create filehandle for string";
    open($out_fh, '>', \$out_str);

    $self->do_main({ lines => 1, eol => '::::' }, $in_fh, 'db.more', $out_fh);

    is($out_str, "1\n2\n3\n", "simple check");

    close $in_fh;
    close $out_fh;

}

########## begin option processing ##########
sub print_usage {
    print STDERR qq{\nUsage: @{[ ($0 =~ m{\A.*/([^/]+)\Z})[0] || $0 ]} options

    Options:
        [-V | --version]  # print version
        [-c <conf_file>]  # path to ini-style config file
        [-f <field_name>] # field name to extract
        [--is <sep>]      # input separator
        [--os <sep>]      # output separator
        [-n]              # print a newline after the value
        [--self-install]  # install somewhere (defaults to /usr/local/bin)
        [--prefix <dir>]  # install prefix (defaults to /usr/local)
        [--bindir <dir>]  # bin dir (defaults to \$prefix/bin)

        [-h | --help]     # this help msg
        [--man]           # display manpage-like documentation (perldoc)
\n};
}

sub check_options {
    my ($self, $opts, $required) = @_;

    if (not $opts or $opts->{help}) {
        $self->print_usage;
        exit 1;
    }

    my $opt_ok = 1;
    $required = [ ] unless $required;
    foreach my $key (@$required) {
        if (defined($opts->{$key})) {
            if (my $v = $opts->{$key}) {
                if (my $ref = ref($v)) {
                    if ($ref eq 'ARRAY' ) {
                        unless (@$v) {
                            $opt_ok = 0;
                            warn "missing required option '$key'
";
                        }
                    }
                }
            }
        }
        else {
            $opt_ok = 0;
            warn "missing required option '$key'\n";
        }
    }

    unless ($opt_ok) {
        $self->print_usage;
        exit 1;
    }

    return $opt_ok;
}

sub get_options {
    my ($self, $spec, $defaults) = @_;
    my %opts = $defaults ? %$defaults : ();
    $spec = [ ] unless $spec;

    my $process_opt = sub {
        my ($key, $val) = @_;

        if (scalar(@_) > 2) {
            $opts{$key}{$val} = $_[2];
        }
        else {
            if ( exists($opts{$key}) and (my $v = $opts{$key}) ) {
                if (my $ref = ref($v)) {
                    if ($ref eq 'ARRAY' ) {
                        push @{ $opts{$key} }, $val;
                        return 1;
                    }
                }
            }

            $opts{$key} = $val;
        }
    };

    my $opt_rv = Getopt::Long::GetOptions(map { ($_ => $process_opt) } @$spec);

    return $opt_rv ? \%opts : undef;
}
########## end option processing ##########

sub get_full_version {
    my ($self) = @_;

    my $prog = ($0 =~ m{\A.*/([^/]+)\Z})[0];

    return "$prog " . $VERSION . "\n";
}

sub self_install {
    my ($self, $prefix, $bin_dir) = @_;

    unless (defined $prefix or defined $bin_dir) {
        $bin_dir = '/usr/local/bin';
    }

    $bin_dir = "$prefix/bin" unless defined $bin_dir;

    my $perl;
    {
        no warnings 'once';
        require Config;
        $perl = $Config::Config{perlpath};
    }

    my $script = __FILE__;

    unless (-w $bin_dir) {
        print STDERR "\ndconfini: can't write to $bin_dir.  ";
        print STDERR "Maybe you need to run me as root?\n\n";
        exit 1;
    }

    my $new_path = "$bin_dir/djoinstr";

    open(my $in_fh, '<', $script) or
        die "couldn't open script source '$script'";
    my $line;

    open(my $out_fh, '>', $new_path)
        or die "couldn't open output file $new_path";

    $line = <$in_fh>; # eat shebang line
    print $out_fh "#!$perl\n";

    while ($line = <$in_fh>) {
        print $out_fh $line;
    }

    close $out_fh;
    close $in_fh;

    chmod 0755, $new_path;

    print STDERR "Installed as $new_path\n";
}

sub do_pod {
    my ($self) = @_;

    my $src = __FILE__;

    my $perl = $self->get_perl_exec;

    require File::Basename;
    my $dir = File::Basename::dirname($perl);
    my $perldoc = "$dir/perldoc";

    $perldoc = "perldoc" unless -x $perldoc;

    if (system $perldoc, $src) {
        print STDERR "dconfini: couldn't run '$perldoc $src': $?\n";
        exit 1;
    }
    exit 0;
}

sub get_perl_exec {
    my $perl;
    {
        no warnings 'once';
        require Config;
        $perl = $Config::Config{perlpath};
    }

    return $perl;
}

=pod

=head1 NAME

dconfjson - Retrieve values from a JSON config file

=head1 SYNOPSIS

  dconfjson /etc/stuff.conf my.conf.field

=head1 DESCRIPTION

=head2 OPTIONS

=cut
